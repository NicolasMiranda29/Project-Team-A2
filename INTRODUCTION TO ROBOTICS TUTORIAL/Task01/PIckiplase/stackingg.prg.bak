Integer Tokens
Integer Blocks
Integer TokenRow
Integer TokenCol
Integer BlockRow
Integer BlockCol
Real TokenHeight
Real BlockHeight
Real tElapsed   ' variable para tiempo

Function main

    Motor On
    Power High
    Speed 30
    Accel 30, 30
    SpeedS 1300
    AccelS 5000
    Tool 1

    Tokens = 3
    Blocks = 3
    TokenHeight = 6.0
    BlockHeight = 6.0

    Pallet 1, Local_Tray_Base, P20, P21, 2, 3
    Pallet 2, Local_Infeed, P23, P22, 1, 2

    Integer TokenID
    Integer BlockID

    Go Retract_Safe

    ' --- INICIO MEDICIÓN ---
    TmReset 0   ' Reinicia el timer #0

    For TokenID = Tokens To 1 Step -1
        Pick_Infeed_Token()
        Alignment_Token()

        TokenRow = 2
        TokenCol = TokenID
        Place_Tray_Token
    Next TokenID

    For BlockID = Blocks To 1 Step -1
        Pick_Infeed_Block()
        Alignment_Block()

        BlockRow = 1
        BlockCol = BlockID
        Place_Tray_Block
    Next BlockID

    ' --- FIN MEDICIÓN ---
    tElapsed = Tmr(0)   ' Tiempo transcurrido en segundos
    Home
    Print "Tiempo total de ejecución = " + Str$(tElapsed) + " seconds"

    
Fend


Function Pick_Infeed_Token
	Print "Picking Token from Infeed. Token ID = ", Tokens
	Go Pallet(2, 1, 2) +Z(50 + (Blocks * BlockHeight)) CP
	Move Pallet(2, 1, 2) +Z(Tokens * TokenHeight)
	On 8
	Wait .5
	Move Pallet(2, 1, 2) +X(-1) +Z(70 + (Tokens * TokenHeight)) CP
Fend

Function Pick_Infeed_Block
	Print "Picking Block from Infeed. Block ID = ", Blocks
	Go Pallet(2, 1, 1) +Z(50 + (Blocks * BlockHeight)) CP
	Move Pallet(2, 1, 1) +Z(Blocks * BlockHeight)
	On 8
	Wait .5
	Move Pallet(2, 1, 1) +X(-1) +Y(1) +Z(70 + (Blocks * BlockHeight)) CP
Fend

Function Alignment_Token
	Print "Aligning Token. Token ID = ", Tokens
	Go Align_Token +Z(50) -Y(2) +X(1) CP
	Move Align_Token -Y(2) -X(1) CP
	Off 8
	Move Align_Token -Y(2) -X(1) +Z(5) CP
	On 10
	Wait .5
	Go Align_Token +Z(5) CP
	Move Align_Token +Z(5) CP
	Off 10
	Go Align_Token CP
	Move Align_Token
	On 8
	Wait .5
	Move Align_Token +Z(50) CP
Fend

Function Alignment_Block
	Print "Aligning Block. Block ID = ", Blocks
	Go Align_Block +Z(50) -Y(2) CP
	Move Align_Block -Y(2) CP
	Off 8
	Move Align_Block -Y(2) +Z(5) CP
	On 10
	Wait .5
	Go Align_Block +Z(5) CP
	Move Align_Block +Z(5) CP
    Off 10
	Move Align_Block -Y(1) CP
	Go Align_Block -Y(1) CP
	Move Align_Block -Y(1)
	On 8
	Wait .5
	Move Align_Block +Z(80) CP
Fend

Function Place_Tray_Token
	Print "Placing Token in Tray at Pallet"
	Go Pallet(1, TokenRow, TokenCol) +Y(2) -X(1) +Z(100) CP
	Move Pallet(1, TokenRow, TokenCol) +Y(2) -X(1) +Z(5)
	Off 8
	Wait .5
	Move Pallet(1, TokenRow, TokenCol) +Y(2) -X(1) +Z(50) CP
	Tokens = Tokens - 1
Fend

Function Place_Tray_Block
	Print "Placing Block in Tray at Pallet(2)"
	Go Pallet(1, BlockRow, BlockCol) -X(1) +Z(100) CP
	Move Pallet(1, BlockRow, BlockCol) -X(1) +Z(5)
	Off 8
	Wait .5
	Move Pallet(1, BlockRow, BlockCol) -X(1) +Z(50) CP
	Blocks = Blocks - 1
Fend

